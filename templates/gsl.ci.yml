.template 0
###############################################################################
# Copyright (c) 2014-2021 libbitcoin developers (see COPYING).
#
# GSL generate libbitcoin .travis.yml.
#
# This is a code generator built using the iMatix GSL code generation
# language. See https://github.com/imatix/gsl for details.
###############################################################################
# Functions
###############################################################################

function workflow_relative_path(repository, path_prefix)
    define my.repository = workflow_relative_path.repository
    require(my.repository, "repository", "name")
    define my.subpath = ".github/workflows"
    return append_path(append_path(my.path_prefix,\
        canonical_path_name(my.repository)), my.subpath)
endfunction

function is_system_linux(job)
    define my.job = is_system_linux.job
    return defined(my.job) & my.job.system = "linux"
endfunction

function is_system_osx(job)
    define my.job = is_system_osx.job
    return defined(my.job) & my.job.system = "osx"
endfunction

function get_job_os(job)
    define my.job = get_job_os.job
    define my.os = ""

    if (is_empty(my.job.system))
        abort "get_job_os failed due to missing job.system value."
    elsif (my.job.system = "linux")
        my.os = "ubuntu-latest"
    elsif (my.job.system = "osx")
        my.os = "macos-latest"
    else
        abort "get_job_os failed due to unsupported job.system value '$(my.job.system)'."
    endif

    return my.os
endfunction #get_job_os

function get_job_packager(job)
    define my.job = get_job_packager.job
    define my.packager = ""

    if (my.job.system = "linux")
        my.packager = "apt"
    elsif (my.job.system = "osx")
        my.packager = "brew"
    else
        abort ("get_job_packager failed due to unsupported job.system value '$(my.job.system)'.")
    endif

    return my.packager
endfunction #get_job_packager

function get_job_packages(job)
    define my.job = get_job_packages.job
    define my.packages = ""
    define my.spacer = ""

    if (my.job.system = "osx")
        if (!is_empty(my.job.boost) & !(my.job.boost = "build"))
            my.packages = "$(my.packages)$(my.spacer)boost"
            my.spacer = " "
        endif

        if (!is_empty(my.job.icu) & !(my.job.icu = "build"))
            my.packages = "$(my.packages)$(my.spacer)icu4c"
            my.spacer = " "
        endif
    elsif (my.job.system = "linux")
        my.packages = get_job_compiler_package(my.job)
    endif

    return "$(my.packages)"
endfunction #get_job_packages(

function get_job_compiler(job)
    define my.job = get_job_compiler.job
    define my.compiler = ""

    if (my.job.system = "osx")
        my.compiler = "clang"

        if (!is_empty(my.job.compiler))
            abort "Continuious Integration 'osx' job has extraneous 'compiler' property."
        endif
    elsif (my.job.system = "linux")
        if (is_empty(my.job.compiler))
            abort "get_job_compiler failed due to missing job.compiler value."
        elsif ((my.job.compiler = "gcc") | (my.job.compiler = "clang"))
            my.compiler = "$(my.job.compiler)"
        else
            abort "get_job_compiler failed due to unsupported job.compiler value $(my.job.compiler)."
        endif
    else
        abort "get_job_compiler unrecognized system value $(my.job.system)"
    endif

    return "$(my.compiler)"
endfunction #get_job_compiler

function get_job_compiler_package(job)
    define my.job = get_job_compiler_package.job
    define my.version = ""

    if (!is_empty(my.job.version))
        my.version = "-$(my.job.version)"
    endif

    return "$(get_job_compiler(my.job))$(my.version)"
endfunction #get_job_compiler_package

function get_job_assert(job)
    define my.job = get_job_assert.job
    define my.parameter = ""

    if (!is_empty(my.job.assert) & (my.job.assert = "false"))
      my.parameter = "--disable-ndebug"
    endif

    return my.parameter
endfunction #get_job_assert

function get_job_cc(job)
    define my.job = get_job_cc.job
    define my.version = ""
    define my.compiler = get_job_compiler(my.job)

    if (!is_empty(my.job.version))
        my.version = "-$(my.job.version)"
    endif

    if !(my.compiler = "gcc") & !(my.compiler = "clang")
        abort "get_job_cc failed due to unsupported compiler value."
    endif

    return "$(my.compiler)$(my.version)"
endfunction #get_job_cc 

function get_job_cxx(job)
    define my.job = get_job_cxx.job
    define my.version = ""
    define my.compiler = ""
    define my.specified_compiler = get_job_compiler(my.job)

    if (!is_empty(my.job.version))
        my.version = "-$(my.job.version)"
    endif

    if (my.specified_compiler = "gcc")
        my.compiler = "g++"
    elsif (my.specified_compiler = "clang")
        my.compiler = "clang++"
    else
        abort "get_job_cxx failed due to unsupported compiler value."
    endif

    return "$(my.compiler)$(my.version)"
endfunction #get_job_cxx 

function get_job_link(job)
    define my.job = get_job_link.job
    define my.option = ""

    if (is_empty(my.job.link))
        abort "get_job_link failed due to missing job.link value."
    elsif (my.job.link = "dynamic")
        my.option = "--disable-static"
    elsif (my.job.link = "static")
        my.option = "--disable-shared"
    else
        abort "get_job_link failed due to unsupported job.link value."
    endif

    return my.option
endfunction #get_job_link

function get_job_cflags(job)
    define my.job = get_job_cflags.job
    define my.flags = ""

    if (is_empty(my.job.optimization))
        abort "get_job_cflags expects job.optimization, no value provided."
    elsif (my.job.optimization = "size")
        my.flags = "-Os"
    elsif (my.job.optimization = "debug")
        my.flags = "-Og"
    else
        abort "get_job_cflags unsupported job.optimization value '$(my.job.optimization)."
    endif

    if (!is_empty(my.job.coverage) & (my.job.coverage = "true"))
        my.flags = "$(my.flags) -g --coverage"
    endif

    return "$(my.flags) -fPIE"
endfunction #get_job_cflags

function get_job_cxxflags(job)
    define my.job = get_job_cxxflags.job
    return get_job_cflags(my.job)
endfunction #get_job_cxxflags

function get_job_installsh_icu(job)
    define my.job = get_job_installsh_icu.job
    define my.flags = ""

    if (is_empty(my.job.icu))
        my.flags = ""
    elsif (my.job.icu = "with")
        my.flags = "--with-icu"
    elsif (my.job.icu = "build")
        my.flags = "--build-icu --with-icu"
    else
        abort "get_job_installsh_icu unrecognized job.icu value."
    endif

    return my.flags
endfunction #get_job_installsh_icu

function get_job_installsh_boost(job)
    define my.job = get_job_installsh_boost.job
    define my.flags = ""

    if (is_empty(my.job.boost))
        my.flags = ""
    elsif (my.job.boost = "with")
        my.flags = ""
    elsif (my.job.boost = "build")
        my.flags = "--build-boost"
    else
        abort "get_job_installsh_boost unrecognized job.boost value."
    endif

    return my.flags
endfunction #get_job_installsh_boost

###############################################################################
# Macros
###############################################################################
.endtemplate
.template 1
.
.macro initialize_matrix_entry(job)
.   define my.job = initialize_matrix_entry.job
.
          - os: $(get_job_os(my.job))
            packager: "$(get_job_packager(my.job))"
            packages: "$(get_job_packages(my.job))"
            compiler: "$(get_job_compiler_package(my.job))"
.   if (!is_empty(my.job.coverage) & (my.job.coverage = "true"))
            coverage: "$(my.job.coverage)"
.   else
            coverage: "false"
.   endif
            assert: "$(get_job_assert(my.job))"
            cc: "$(get_job_cc(my.job))"
            cflags: "$(get_job_cflags(my.job))"
            cxx: "$(get_job_cxx(my.job))"
            cxxflags: "$(get_job_cxxflags(my.job))"
            link: "$(get_job_link(my.job))"
            icu: "$(get_job_installsh_icu(my.job))"
            boost: "$(get_job_installsh_boost(my.job))"

.endmacro # initialize_matrix_entry
.
.macro initialize_verify_matrix(matrix)
.   define my.matrix = initialize_verify_matrix.matrix
.
      matrix:
        include:
.
.   for my.matrix.job by "$(compiler)-$(link)" as _job where is_system_linux(_job)
.       initialize_matrix_entry(_job)
.   endfor _job
.
.   for my.matrix.job by "$(link)" as _job where is_system_osx(_job)
.       initialize_matrix_entry(_job)
.   endfor _job
.
.endmacro #initialize_verify_matrix
.
.endtemplate
.template 0
###############################################################################
# Generation
###############################################################################
.endtemplate
.template 1
.macro generate_ci_yml(path_prefix)
.for generate.repository by name as _repository
.   require(_repository, "repository", "name")
.   if (defined(_repository->ci))
.       my.absolute_path = join(join(global.root, my.path_prefix), _repository.name)
.       my.output_path = workflow_relative_path(_repository, my.path_prefix)
.       create_directory(my.output_path)
.       define my.out_file = "$(my.output_path)/ci.yml"
.       notify(my.out_file)
.       output(my.out_file)
.       copyleft(_repository.name)
.       define my.matrix = _repository->ci

name: Continuous Integration Build

on: [ pull_request, push, workflow_dispatch ]

jobs:
  verify-installsh:

    strategy:
      fail-fast: false

.       if (count(my.matrix.job, (count.system = "linux")) + count(my.matrix.job, (count.system = "osx"))  > 0)
.           initialize_verify_matrix(my.matrix)
.       endif
    runs-on: ${{ matrix.os }}

    env:
      CC: ${{ matrix.cc }}
      CFLAGS: '${{ matrix.cflags }}'
      CXX: ${{ matrix.cxx }}
      CXXFLAGS: '${{ matrix.cxxflags }}'
      CI_REPOSITORY: '${{ github.repository }}'

    steps:
      - name: Checkout repository
        uses: actions/checkout@v2

      - name: Prepare toolchain [apt]
        if: ${{ matrix.packager == 'apt' }}
        run: |
          sudo apt-get update
          sudo apt-get install git build-essential autoconf automake libtool pkg-config ${{ matrix.packages }}

      - name: Prepare toolchain [brew]
        if: ${{ matrix.packager == 'brew' }}
        run: |
          brew install git autoconf automake libtool pkg-config ${{ matrix.packages }}

      - name: Execute install.sh
        run: >
          ./install.sh
          ${{ matrix.link }}
          ${{ matrix.assert }}
          ${{ matrix.icu }}
          ${{ matrix.boost }}
          --build-dir=${{ github.workspace }}/build/
          --prefix=${{ github.workspace }}/dependencies/

      - name: Coveralls Calculation [apt-only]
        if: ${{ (matrix.packager == 'apt') && (matrix.coverage == 'true') }}
        run: |
          sudo apt-get install lcov
          lcov --directory . --capture --output-file coverage.info
          lcov --remove coverage.info "/usr/*" "${{ github.workspace }}/dependencies/*" "${{ github.workspace }}/build/*" "${{ github.workspace }}/examples/*" "${{ github.workspace }}/test/*" --output-file coverage.info
          lcov --list coverage.info

      - name: Coveralls.io Upload
        if: ${{ matrix.coverage == 'true' }}
        uses: coverallsapp/github-action@master
        with:
          path-to-lcov: "./coverage.info"
          github-token: ${{ secrets.github_token }}

      - name: Display boost bootstrap.log [--build-boost]
        if: ${{ failure() && (matrix.boost == '--build-boost') }}
        run: |
          gcc -v
          clang -v
          cat ${{ github.workspace }}/build/build-*/bootstrap.log

.       close
.   endif
.endfor _repository
.endmacro # generate_ci_yml
.endtemplate
.template 0
###############################################################################
# Execution
###############################################################################
[global].root = ".."
[global].trace = 0
[gsl].ignorecase = 0

# Note: expected context root libbitcoin-build directory
gsl from "library/math.gsl"
gsl from "library/string.gsl"
gsl from "library/collections.gsl"
gsl from "utilities.gsl"

generate_ci_yml("output")

.endtemplate
